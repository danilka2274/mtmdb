plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
    id 'com.google.gms.google-services'
}

android {
    namespace 'ru.geekbrains.mtmdb'
    compileSdk 34

    defaultConfig {
        applicationId "ru.geekbrains.mtmdb"
        minSdk 24
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "String", "TYPE", '"RELEASE"'
        }
        debug {
            versionNameSuffix "-debug"
            buildConfigField "String", "TYPE", '"DEBUG"'
        }
        beta {
            initWith debug
            versionNameSuffix "-beta"
            buildConfigField "String", "TYPE", '"BETA"'
        }
    }
    buildTypes.each {
        Properties properties = new Properties()
        properties.load(project.rootProject.file("apiconfig.properties").newDataInputStream())
        def apiKey = properties.getProperty("tmdb_api_key", "")
        it.buildConfigField "String", "TMDB_API_KEY", apiKey
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding true
        buildConfig = true
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:2.0.0-Beta1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.3'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3'

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    //ViewModel + LiveData
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.7.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.7.0'

    //Koin for Kotlin Multiplatform
    implementation 'io.insert-koin:koin-android:3.2.0'

    //GSON
    implementation 'com.google.code.gson:gson:2.10'

    //Coil
    implementation 'io.coil-kt:coil:1.3.0'

    //Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'

    //Room
    implementation 'androidx.room:room-runtime:2.6.1'
    kapt 'androidx.room:room-compiler:2.6.1'

    //Firebase messaging
    implementation 'com.google.firebase:firebase-messaging-ktx:22.0.0'

    //Google map
    implementation 'com.google.android.gms:play-services-maps:17.0.1'

    implementation 'androidx.legacy:legacy-support-v4:1.0.0'


    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}